options:
    prefix: &4&lBOSS &7| &a
    bossname: &c&lKai
    loc_x: -327.487
    loc_y: 125
    loc_z: 411.355
    loc_world: flatroom

on load:
    set {_world} to world "{@loc_world}"
    set {bossLoc} to location at {@loc_x}, {@loc_y}, {@loc_z} in {_world}
    kill {pb}
    delete {pb}
    set {PayBoxDrop1} to red dye named "&c&lKai's Sword Shard" with lore "&4Dropped By Kai"

function PayBox1(l: location):
    if {pb} is not set:
        summon adult zombie at {_l}
        set {pb} to last spawned zombie
        set {_h} to skull of "Garfle" parsed as offline player
        set {_c} to leather chestplate
        set {_l} to leather leggings
        set {_b} to leather boots
        dye {_c} red
        dye {_l} red
        dye {_b} red
        enchant {_c} with protection 10
        enchant {_l} with protection 10
        enchant {_b} with protection 10
        set helmet of {pb} to {_h}
        set chestplate of {pb} to {_c}
        set leggings of {pb} to {_l}
        set boots of {pb} to {_b}
        set tool of {pb} to diamond sword of sharpness 40
        set max health of {pb} to 800
        heal {pb}
        set display name of {pb} to "{@bossname} &c%health of {pb}%/%max health of {pb}%❤"
        broadcast "{@prefix}{@bossname} has spawned!"
        play sound "block.end_portal.spawn" to all players

command /spawneventboss:
    permission: op
    trigger:
        PayBox1(player's location):

every 20 minutes:
    PayBox1({bossLoc})

on damage:
    if victim is {pb}:
        set display name of {pb} to "{@bossname} &c%health of {pb}%/%max health of {pb}%❤"

on death:
    if victim is {pb}:
        clear drops
        delete {pb}
        play sound "block.respawn_anchor.set_spawn" to all players
        broadcast "{@prefix}{@bossname} &6has been killed by &d%attacker%!"
        drop {PayBoxDrop1} 

every 5 seconds:
    if {pb} is set:
        set {_c} to a random integer between 1 and 5
        if {_c} is 1: #Pull Players
            loop all players in radius 7 of {pb}:
                if loop-player's gamemode is survival:
                    push loop-player forward at speed 2
                    apply slowness of tier 5 to loop-player for 4 seconds
                    play sound "entity.llama.swag" to loop-player
        if {_c} is 2: #Wither + Blindness
            loop all players in radius 5 of {pb}:
                if loop-player's gamemode is survival:
                    apply wither of tier 3 to loop-player for 1 seconds
                    apply blindness of tier 2 to loop-player for 3 seconds
                    play sound "entity.wither_skeleton.hurt" to loop-player
        if {_c} is 3: #Knockback + Wither:
            loop all players in radius 5 of {pb}:
                if loop-player's gamemode is survival:
                    push loop-player up at speed 4
                    push loop-player backwards at speed 6
                    play sound "entity.wither_skeleton.hurt" to loop-player
                    apply wither of tier 2 to loop-player for 5 seconds
        if {_c} is 4: #Syphon
            loop all players in radius 5 of {pb}:
                if loop-player's gamemode is survival:
                    damage loop-player by 3 hearts
                    apply blindness of tier 2 to loop-player for 4 seconds
                    play sound "entity.player.attack.crit" to loop-player
                    send "{@prefix}{@bossname} &bhas syphoned 3 hearts of your health!" to loop-player
            heal {pb} by 15 hearts
        if {_c} is 5: #Buff
            loop all players in radius 6 of {pb}:
                if loop-player's gamemode is survival:
                    apply strength of tier 4 to {pb} for 7 seconds
                    apply weakness of tier 3 to loop-player for 7 seconds
                    play sound "entity.splash_potion.break" to loop-player