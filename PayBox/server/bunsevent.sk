options:
    prefix: &4&lBOSS &7| &a
    bossname: &c&lLyvt
    loc1_x: 70.895
    loc1_y: 149
    loc1_z: 363.497
    loc_world: flatroom

on load:
    set {_world} to world "{@loc_world}"
    set {lyvtLoc} to location at {@loc1_x}, {@loc1_y}, {@loc1_z} in {_world}
    kill {lb}
    delete {lb}

function Lyvt1(l: location):
    if {lb} is not set:
        summon adult zombie at {_l}
        set {lb} to last spawned zombie
        set {_h} to skull of "lvshe" parsed as offline player
        set {_c} to leather chestplate
        set {_l} to leather leggings
        set {_b} to leather boots
        dye {_c} red
        dye {_l} red
        dye {_b} red
        enchant {_c} with protection 20
        enchant {_l} with protection 20
        enchant {_b} with protection 20
        set helmet of {lb} to {_h}
        set chestplate of {lb} to {_c}
        set leggings of {lb} to {_l}
        set boots of {lb} to {_b}
        set tool of {lb} to diamond sword of sharpness 100
        set max health of {lb} to 3000
        heal {lb}
        set display name of {lb} to "{@bossname} &c%health of {lb}%/%max health of {lb}%❤"
        broadcast "{@prefix}{@bossname} has spawned!"
        play sound "block.end_portal.spawn" to all players

command /spawneventboss:
    permission: op
    trigger:
        Lyvt1({lyvtLoc}):


on damage:
    if victim is {lb}:
        set display name of {lb} to "{@bossname} &c%health of {lb}%/%max health of {lb}%❤"

on death:
    if victim is {lb}:
        clear drops
        delete {lb}
        play sound "block.respawn_anchor.set_spawn" to all players
        broadcast "{@prefix}{@bossname} &6has been killed by &d%attacker%!"

every 5 seconds:
    if {lb} is set:
        set {_c} to a random integer between 1 and 5
        if {_c} is 1: #Pull Players
            loop all players in radius 7 of {lb}:
                if loop-player's gamemode is survival:
                    push loop-player forward at speed 2
                    apply slowness of tier 5 to loop-player for 4 seconds
                    play sound "entity.llama.swag" to loop-player
        if {_c} is 2: #Wither + Blindness
            loop all players in radius 5 of {lb}:
                if loop-player's gamemode is survival:
                    apply wither of tier 3 to loop-player for 1 seconds
                    apply blindness of tier 2 to loop-player for 3 seconds
                    play sound "entity.wither_skeleton.hurt" to loop-player
        if {_c} is 3: #Knockback + Wither:
            loop all players in radius 5 of {lb}:
                if loop-player's gamemode is survival:
                    push loop-player up at speed 4
                    push loop-player backwards at speed 6
                    play sound "entity.wither_skeleton.hurt" to loop-player
                    apply wither of tier 2 to loop-player for 5 seconds
        if {_c} is 4: #Syphon
            loop all players in radius 5 of {lb}:
                if loop-player's gamemode is survival:
                    damage loop-player by 3 hearts
                    apply blindness of tier 2 to loop-player for 4 seconds
                    play sound "entity.player.attack.crit" to loop-player
                    send "{@prefix}{@bossname} &bhas syphoned 3 hearts of your health!" to loop-player
            heal {lb} by 15 hearts
        if {_c} is 5: #Buff
            loop all players in radius 6 of {lb}:
                if loop-player's gamemode is survival:
                    apply strength of tier 4 to {lb} for 7 seconds
                    apply weakness of tier 3 to loop-player for 7 seconds
                    play sound "entity.splash_potion.break" to loop-player